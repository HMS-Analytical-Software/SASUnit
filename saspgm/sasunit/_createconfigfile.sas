/**
   \file
   \ingroup    SASUNIT_SETUP

   \brief      Creates a SAS config file for starting SASUnit in batch

   \version    \$Revision$
   \author     \$Author$
   \date       \$Date$
   
   \sa         For further information please refer to https://github.com/HMS-Analytical-Software/SASUnit/wiki/User%27s%20Guide/
               Here you can find the SASUnit documentation, release notes and license information.
   \sa         \$HeadURL$
   \copyright  Copyright 2010-2023 HMS Analytical Software GmbH, http://www.analytical-software.de
               This file is part of SASUnit, the Unit testing framework for SAS(R) programs.
               For copyright information and terms of usage under the GNU Lesser General Public License see included file README.md
               or https://github.com/HMS-Analytical-Software/SASUnit/wiki/readme/.
               
   \param      i_projectBinFolder      Name of the folder contain the binary files an shell scripts to start SASUnit (usually bin).
                                       <BR>Signifies the location of the config file.
   \param      i_projectRootFolder     Path to the project root folder used to resolve the complete path of i_sasunitRunAllPgm.
   \param      i_sasunitLogFolder      Name of the folder where the log files should be stored.
   \param      i_sasunitLanguage       Language that should be used in the SAS session. It is also part of the name of the config file.
   \param      i_sasunitRunAllPgm      Name of the program that starts all scenarios, containing calls \%initSASUnit, \%runSASUnit and \%reportSASUnit.
   \param      i_operatingSystem       Name of the operating system under which SAS is running. It is also part of the name of the config file.
   \param      i_sasVersion            SAS Version that is used to run SASUnit. SAS Version is part of the path to SAS provided config file and it is also part ot the name of the config file.
   \param      i_sasConfig             Path and name of the of the SAS provided config file that should be used to run SASUnit
*/ /** \cond */
%macro _createConfigFile(i_projectBinFolder =
                        ,i_projectRootFolder=
                        ,i_sasunitLogFolder=
                        ,i_sasunitLanguage  =
                        ,i_sasunitRunAllPgm =
                        ,i_operatingSystem  =
                        ,i_sasVersion       =
                        ,i_sasConfig        =
                        );

   %local
     l_sasConfig
	  l_sasunitRunAllPgm
     l_sasunitLogFolder
   ;

   %let l_sasConfig           =%_makeSASUnitpath(&i_sasConfig.);
   %let l_sasunitRunAllPgm    =%_makeSASUnitpath(&i_sasunitRunAllPgm.);
   %let l_sasunitRunAllPgm    =%_stdPath(&i_projectRootFolder., &l_sasunitRunAllPgm.);
   %let l_sasunitLogFolder    =%_makeSASUnitpath(&i_sasunitLogFolder.);

   data _NULL_;
      file "&i_projectBinFolder./sasunit.&i_sasVersion..&i_operatingSystem..&i_sasunitLanguage..cfg";
      put "/**";
      put "   \copyright  This file is part of SASUnit, the Unit testing framework for SAS(R) programs.";
      put "               For copyright information and terms of usage under the Lesser GPL license see included file readme.txt";
      put "               or https://github.com/HMS-Analytical-Software/SASUnit/wiki/readme.";
      put;
      put "   \author generated by SASUnit through &sysuserid.";
      put "   \date generated on %sysfunc(putn (%sysfunc(datetime()), nldatm.))";
      put "*/";
      put;
      put %sysfunc (quote (-CONFIG %_adaptSASUnitPathToOS(&l_sasConfig.)));
      put %sysfunc (quote (-sysin  %_adaptSASUnitPathToOS(&l_sasunitRunAllPgm.)));
      put %sysfunc (quote (-log    %_adaptSASUnitPathToOS(&l_sasunitLogFolder.)));
      put %sysfunc (quote (-print  %_adaptSASUnitPathToOS(&l_sasunitLogFolder.)));
   run;

%mend _createConfigFile;
/** \endcond */